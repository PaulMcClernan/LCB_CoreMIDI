
private variable sRequestPermissionsCompletionHandler as optional ObjcBlockPointer
private variable sTarget as ScriptObject

public handler AudioLibraryInitialize() returns Boolean
   if not CreateObjcBlockPointerFromHandler(RequestPermissionsCompletionHandler, sRequestPermissionsCompletionHandler) then
      put nothing into sRequestPermissionsCompletionHandler
      return false
   end if
   put the caller into sTarget
   return true
end handler

public handler RequestPermissionsCompletionHandler(in pBlock as ObjcBlockPointer, in pGranted as CBool)
   post "AudioLibraryRequestPermissionsCallback" to sTarget with [pGranted]
end handler

private foreign handler ObjC_AVCaptureDeviceRequestAccessForMediaType(in pMediaType as ObjcId, in pCompletionHandler as ObjcBlockPointer) returns nothing \
                           binds to "objc:AVCaptureDevice.+requestAccessForMediaType:completionHandler:"

public handler AudioLibraryRequestPermissions()
   unsafe
      ObjC_AVCaptureDeviceRequestAccessForMediaType(StringToNSString("soun"), sRequestPermissionsCompletionHandler)
   end unsafe
end handler

public handler AudioLibraryFinalize()
   if sRequestPermissionsCompletionHandler is not nothing then
      DeleteObjcBlockPointer(sRequestPermissionsCompletionHandler)
      put nothing into sRequestPermissionsCompletionHandler
   end if
end handler
